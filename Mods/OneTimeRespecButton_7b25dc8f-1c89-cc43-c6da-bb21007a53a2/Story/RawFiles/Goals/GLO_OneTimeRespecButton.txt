Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_OTR_TimerPrefix("AddButtonTimer_");
KBSECTION
IF
DB_PartyMembers(_Character)
AND
IsPlayer(_Character, 1)
AND
DB_Origins(_Character)  // not Tav or Durge!
AND
GUIDToString(_Character, _onlyOnceStr1)
AND
Concatenate(_onlyOnceStr1, "OneTimeRespecButtonAdd", _onlyOnceStr)
AND
DB_OTR_TimerPrefix(_TimerPrefix)
AND
ConcatenateGUID(_TimerPrefix, _Character, _AddButtonTimerStr)
AND
QRY_OnlyOnce(_onlyOnceStr)
THEN
AddSpell(_Character, "Shout_OneTimeRespec", 0, 0);
TimerLaunch(_AddButtonTimerStr, 1000);  // Fix for https://github.com/slafniy/OneTimeRespecButton/issues/1

IF
CastedSpell((CHARACTER)_Player, "Shout_OneTimeRespec", _, _, _)
AND
DB_PartyMembers(_Player)
THEN
//DebugText(_Player, "One time respec activated!");
StartRespec(_Player);

IF
RespecCompleted((CHARACTER)_Player)
THEN
//DebugText(_Player, "One time respec used, removing spell!");
RemoveSpell(_Player, "Shout_OneTimeRespec", 0);

IF
LeveledUp((CHARACTER)_Player)
AND
DB_OTR_TimerPrefix(_TimerPrefix)
AND
ConcatenateGUID(_TimerPrefix, _Player, _TimerStr)
AND
TimerExists(_TimerStr, 0)  // To prevent instant button removing if joined party member instantly levels up
THEN
//DebugText(_Player, "Leveled up, removing spell!");
RemoveSpell(_Player, "Shout_OneTimeRespec", 0);
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "ModWrapper_Gustav"
